
Процедура УстановкаПараметровСеанса ( ТребуемыеПараметры )
	
	Если ( ТребуемыеПараметры = Неопределено ) Тогда
		Возврат;		
	КонецЕсли;
	Для Каждого параметр Из ТребуемыеПараметры Цикл
		Если ( параметр = "ТекущийПользователь" ) Тогда
			установитьТекущегоПользователя ();	
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

Процедура установитьТекущегоПользователя ()
	
	пользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь ();
	текущийПользователь = Справочники.Пользователи.НайтиПоРеквизиту ( "ИдентификаторПользователяИБ", пользовательИБ.УникальныйИдентификатор );
	Если ( текущийПользователь.Пустая () ) Тогда
		текущийПользователь = создатьПользователя ();
	КонецЕсли;
	ПараметрыСеанса.ТекущийПользователь = текущийПользователь;
				
КонецПроцедуры

Функция создатьПользователя ()
	
	УстановитьПривилегированныйРежим ( Истина );
	пользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь ();
	пользователь = Справочники.Пользователи.СоздатьЭлемент ();
	пользователь.Наименование = пользовательИБ.Имя;
	пользователь.НаименованиеПолное = пользовательИБ.ПолноеИмя;
	пользователь.ИдентификаторПользователяИБ = пользовательИБ.УникальныйИдентификатор;
	Если ( ПараметрыСеанса.КонтрагентИспользование ) Тогда
		пользователь.Контрагент = ПараметрыСеанса.Контрагент;
	КонецЕсли; 
	пользователь.Записать ();
	УстановитьПривилегированныйРежим ( Ложь );
	Возврат пользователь.Ссылка;

КонецФункции 