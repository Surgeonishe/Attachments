
Процедура ВыбратьФайлы ( ОповещениеВозврата ) Экспорт
	
	п = Новый Структура ();
	п.Вставить ( "ОповещениеВозврата", ОповещениеВозврата );
	диалог = ПолучитьДиалогВыбораФайлов ();
    завершение = Новый ОписаниеОповещения ( "ЗавершениеЗагрузки", ЭтотОбъект, п );
    прогресс = Новый ОписаниеОповещения ( "ХодЗагрузки", ЭтотОбъект );
	передНачалом = Новый ОписаниеОповещения ( "ПередНачаломЗагрузки", ЭтотОбъект );
    НачатьПомещениеФайловНаСервер ( завершение, прогресс, передНачалом, диалог, Новый УникальныйИдентификатор () );
	
КонецПроцедуры

Функция ПолучитьДиалогВыбораФайлов () Экспорт
	
	диалог = Новый ПараметрыДиалогаПомещенияФайлов ();
	диалог.МножественныйВыбор = Истина;
	диалог.Заголовок = НСтр ( "ru = 'Выберите файл';" );
   	диалог.Фильтр = ПолучитьФильтрФайлов ();
	Возврат диалог;

КонецФункции

Функция ПолучитьФильтрФайлов () Экспорт
	
	фильтр = НСтр ( "ru = 'Файлы (*.gif;*.jpg;*.jpeg;*.png;*.tiff;*.pdf;*.xml;*.txt)|*.gif;*.jpg;*.jpeg;*.png;*.tiff;*.pdf;*.xml;*.txt'" );
	Возврат фильтр;

КонецФункции

Процедура ЗавершениеЗагрузки ( ОписаниеФайлов, Параметры ) Экспорт

	Если ОписаниеФайлов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	файлы = Новый Массив ();
	Для Каждого описание Из ОписаниеФайлов Цикл
		п = Новый Структура ();
		п.Вставить ( "Адрес", описание.Адрес );
		п.Вставить ( "Имя", описание.СсылкаНаФайл.Имя);
		п.Вставить ( "Расширение", описание.СсылкаНаФайл.Расширение );
		п.Вставить ( "Размер", описание.СсылкаНаФайл.Размер () );
		файлы.Добавить ( п );
	КонецЦикла;
	данные = Новый Структура ();
	данные.Вставить ( "Файлы", файлы );
	ВыполнитьОбработкуОповещения ( Параметры.ОповещениеВозврата, данные );
				
КонецПроцедуры

Процедура ХодЗагрузки ( ПомещаемыйФайл, Процент, Отказ, ПроцентВсего, ОтказВсего, Параметры ) Экспорт

	Если ( ОтказВсего ) Тогда
		// код ...	
	Иначе
		с = СтрШаблон ( НСтр ( "ru = 'Загрузка файла %1'" ), ПомещаемыйФайл.Имя );
		пояснение = СтрШаблон ( Нстр ( "ru = 'Размер файла %1 байт'" ), ПомещаемыйФайл.Размер () );
		Состояние ( с, ПроцентВсего, пояснение,  );
	КонецЕсли;

КонецПроцедуры

Процедура ПередНачаломЗагрузки ( ПомещаемыеФайлы, Отказ, Параметры ) Экспорт
	
	максимум = 100 * 1024 * 1024; // пока лимит в 100 МБайт
	Для Каждого помещаемыйФайл Из ПомещаемыеФайлы Цикл
		размер = помещаемыйФайл.Размер ();
		Если ( размер > максимум ) Тогда
			Отказ = Истина;
			с = СтрШаблон ( НСтр ( "ru = 'Отказ. Загружаемый файл «%1» имеет размер более 1 мегабайта'"), помещаемыйФайл.Имя );
			Сообщение = Новый СообщениеПользователю ();
			Сообщение.Текст = с;
			Сообщение.Сообщить ();
		КонецЕсли;
	КонецЦикла;	
			
КонецПроцедуры