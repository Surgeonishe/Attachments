
&НаСервере
Процедура ПриСозданииНаСервере ( Отказ, СтандартнаяОбработка )
	
	Если ( Объект.Ссылка.Пустая () ) Тогда
		начальноеЗаполнение  ();		
	КонецЕсли;
	установитьЗаголовок  ();
	
КонецПроцедуры

&НаСервере
Процедура начальноеЗаполнение ()
	
	Объект.Автор = ПараметрыСеанса.ТекущийПользователь;
	ХранениеФайлов = Константы.ХранениеФайлов.Получить ();
	
КонецПроцедуры

&НаСервере
Процедура установитьЗаголовок ()
	
	с = Документы.Вложение.ПолучитьПредставлениеПоВиду ( Объект.ВидВложения );
	Если ( Объект.Ссылка.Пустая () ) Тогда
		Заголовок = с + " (создание)";
	Иначе
		Заголовок = с + " от " + Формат ( Объект.Дата, "ДФ=dd.MM.yyyy" ) + " номер " + Объект.Номер;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии ( Отказ )
	
	// код ...
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложение ( Команда )
	
	Если ( Объект.Ссылка.Пустая () ) Тогда
		ПоказатьВопрос ( Новый ОписаниеОповещения ( "ВопросЗаписать", ЭтотОбъект ), "Документ еще не записан. Записать?", РежимДиалогаВопрос.ДаНет );
	Иначе
		выбратьФайлы ();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаписать ( Ответ, Парамы ) Экспорт
	
	Если ( Ответ = КодВозвратаДиалога.Да ) Тогда
		Записать ();
		выбратьФайлы ();
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура выбратьФайлы ()
	
	оповещение = Новый ОписаниеОповещения ( "ВыборФайлов", ЭтотОбъект );
	РаботаСФайлами.ВыбратьФайлы ( оповещение );	
			
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлов ( Данные, Парамы ) Экспорт
	
	Если ( Данные = Неопределено ) Тогда
		Возврат;
	Иначе
		добавитьВложения ( Данные.Файлы );
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура добавитьВложения ( Файлы )
	
	Для Каждого п Из Файлы Цикл
		новая = Объект.Файлы.Добавить ();
		новая.Описание = п.Имя;
		м = Объект.Файлы.Выгрузить ( , "КлючСтроки" );
		новая.КлючСтроки = получитьКлюч ( м );
		записатьФайлВРегистр ( п, Объект.Ссылка, новая.КлючСтроки );	
	КонецЦикла;
			
КонецПроцедуры

&НаСервереБезКонтекста
Функция получитьКлюч ( Ключи )
	
	Возврат Справочники.КлючиСтрок.ПолучитьНовыйКлюч ( Ключи );

КонецФункции

&НаСервереБезКонтекста
Процедура записатьФайлВРегистр ( Данные, Объект, КлючСтроки )
	
	п = Новый Структура ();
	п.Вставить ( "Объект", Объект );
	п.Вставить ( "КлючСтроки", КлючСтроки );
	п.Вставить ( "Дата", ТекущаяДатаСеанса () );
	п.Вставить ( "Размер", Данные.Размер );
	п.Вставить ( "Автор", ПараметрыСеанса.ТекущийПользователь );
	п.Вставить ( "Папка", "" );
	п.Вставить ( "ИмяФайла", Данные.Имя );
	п.Вставить ( "Расширение", Данные.Расширение );
	п.Вставить ( "Адрес", Данные.Адрес );
	РегистрыСведений.Файлы.ЗаписатьФайл ( п );
	
КонецПроцедуры