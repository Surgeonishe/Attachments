
Процедура ВыбратьФайлы ( ОповещениеВозврата ) Экспорт
	
	п = Новый Структура ();
	п.Вставить ( "ОповещениеВозврата", ОповещениеВозврата );
	диалог = ПолучитьДиалогВыбораФайлов ();
    завершение = Новый ОписаниеОповещения ( "ЗавершениеЗагрузки", ЭтотОбъект, п );
    прогресс = Новый ОписаниеОповещения ( "ХодЗагрузки", ЭтотОбъект );
	передНачалом = Новый ОписаниеОповещения ( "ПередНачаломЗагрузки", ЭтотОбъект );
    НачатьПомещениеФайловНаСервер ( завершение, прогресс, передНачалом, диалог, Новый УникальныйИдентификатор () );
	
КонецПроцедуры

Функция ПолучитьДиалогВыбораФайлов () Экспорт
	
	диалог = Новый ПараметрыДиалогаПомещенияФайлов ();
	диалог.МножественныйВыбор = Истина;
	диалог.Заголовок = НСтр ( "ru = 'Выберите файл (ы)';" );
   	диалог.Фильтр = ПолучитьФильтрФайлов ();
	Возврат диалог;

КонецФункции

Функция ПолучитьФильтрФайлов () Экспорт
	
	фильтр = НСтр ( "ru = 'PDF '; en = 'PDF '; ua = 'PDF '" ) + "(*.pdf)|*.pdf|"
		+ НСтр ( "ru = 'Word '; en = 'Word '; ua = 'Word '" ) + "(*.doc;*.docx)|*.doc;*docx|"
		+ НСтр ( "ru = 'Excel '; en = 'Excel '; ua = 'Excel '" ) + "(*.xls;*.xlsx)|*.xls;*xlsx|"
		+ НСтр ( "ru = 'Текст '; en = 'Text '; ua = 'Текст '" ) + "(*.txt)|*.txt|"
		+ НСтр ( "ru = 'XML '; en = 'XML '; ua = 'XML '" ) + "(*.xml)|*.xml|"
		+ НСтр ( "ru = 'Изображения '; en = 'Images '; ua = 'Зображення '" ) + "(*.gif;*.jpg;*.jpeg;*.png;*.tiff)|*.gif;*.jpg;*.jpeg;*.png;*.tiff|"
		+ НСтр ( "ru = 'Все файлы '; en = 'All types '; ua = 'Усі файли '" ) + "(*.gif;*.jpg;*.jpeg;*.png;*.tiff;*.pdf;*.xml;*.txt;*.doc;*.docx;*.xls;*.xlsx)|*.gif;*.jpg;*.jpeg;*.png;*.tiff;*.pdf;*.xml;*.txt;*.doc;*.docx;*.xls;*.xlsx|";
	Возврат фильтр;

КонецФункции

Процедура ЗавершениеЗагрузки ( ОписаниеФайлов, Параметры ) Экспорт

	Если ОписаниеФайлов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	файлы = Новый Массив ();
	Для Каждого описание Из ОписаниеФайлов Цикл
		п = Новый Структура ();
		п.Вставить ( "Адрес", описание.Адрес );
		п.Вставить ( "Имя", описание.СсылкаНаФайл.Имя);
		п.Вставить ( "Расширение", описание.СсылкаНаФайл.Расширение );
		п.Вставить ( "Размер", описание.СсылкаНаФайл.Размер () );
		файлы.Добавить ( п );
	КонецЦикла;
	данные = Новый Структура ();
	данные.Вставить ( "Файлы", файлы );
	ВыполнитьОбработкуОповещения ( Параметры.ОповещениеВозврата, данные );
				
КонецПроцедуры

Процедура ХодЗагрузки ( ПомещаемыйФайл, Процент, Отказ, ПроцентВсего, ОтказВсего, Параметры ) Экспорт

	Если ( ОтказВсего ) Тогда
		// код ...	
	Иначе
		с = СтрШаблон ( НСтр ( "ru = 'Загрузка файла %1'" ), ПомещаемыйФайл.Имя );
		пояснение = СтрШаблон ( Нстр ( "ru = 'Размер файла %1 байт'" ), ПомещаемыйФайл.Размер () );
		Состояние ( с, ПроцентВсего, пояснение,  );
	КонецЕсли;

КонецПроцедуры

Процедура ПередНачаломЗагрузки ( ПомещаемыеФайлы, Отказ, Параметры ) Экспорт
	
	максимум = 100 * 1024 * 1024; // пока лимит в 100 МБайт
	Для Каждого помещаемыйФайл Из ПомещаемыеФайлы Цикл
		размер = помещаемыйФайл.Размер ();
		Если ( размер > максимум ) Тогда
			Отказ = Истина;
			с = СтрШаблон ( НСтр ( "ru = 'Отказ. Загружаемый файл «%1» имеет размер более 1 мегабайта'"), помещаемыйФайл.Имя );
			Сообщение = Новый СообщениеПользователю ();
			Сообщение.Текст = с;
			Сообщение.Сообщить ();
		КонецЕсли;
	КонецЦикла;	
			
КонецПроцедуры

Функция ПолучитьШаблонHTML ( ИмяФайла ) Экспорт
	
	расширение = НРег ( Прав ( ИмяФайла, 4 ) );
	шаблон = "";
	Если ( расширение = ".pdf" ) Тогда
		шаблон = получитьШаблонPDF ();
	ИначеЕсли ( расширение = ".xml" ИЛИ расширение = ".txt" ) Тогда
		шаблон = получитьШаблонТекст ();
	ИначеЕсли ( расширение = ".jpg" ИЛИ расширение = "jpeg" ИЛИ расширение = ".gif" ИЛИ расширение = ".png" ИЛИ расширение = ".tif" ) Тогда
		шаблон = получитьШаблонИзображение ();
	КонецЕсли; 
	Возврат шаблон;

КонецФункции

Функция получитьШаблонPDF ()
	
	//с = "<html><style>h3 a{color: #3366FF;}</style><body> <h3 align=""center"">ПРОСМОТР ВРЕМЕННО НЕДОСТУПЕН!</h1><h3 align=""center""><a href=""link.html"">ДЛЯ ПРОСМОТРА ФАЙЛА В ОТДЕЛЬНОМ ОКНЕ НАЖМИТЕ НА ССЫЛКУ.</h1></body></html>";
	// с = "<HTML><iframe height=""#height#%"" src=""file:\\#path#"" width=""#width#%""></iframe></BODY></HTML>";
	с = "<HTML><img height=""#height#%"" src=""file:\\#path#"" width=""#width#%""></iframe></BODY></HTML>";
	Возврат с;
	
КонецФункции

Функция получитьШаблонТекст ()
	
	с = "<html><body> <pre style=""word-wrap: break-word; white-space: pre-wrap;"">#text#</pre></body></html>";
	Возврат с;
	
КонецФункции

Функция получитьШаблонИзображение ()
	
	с = "<HTML><img src=""file:\\#path#"" width=""100%"" ></img></BODY></HTML>";
	// с = "<HTML><img src=""file:\\#path#""></img></BODY></HTML>";
	Возврат с
	
КонецФункции